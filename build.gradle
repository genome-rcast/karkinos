apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'eclipse'

group = 'karkinos'
version = '2.0.5-SNAPSHOT'

description = """"""

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
     maven { url "http://repo.maven.apache.org/maven2" }
}

sourceSets {
    utils {
        compileClasspath += sourceSets.main.output
        java {
            srcDirs = ['src/utils/java']
        }
        resources {
            srcDirs = ['src/utils/resources']
        }
    }
}

configurations {
    utilsCompile {
        extendsFrom compile
    }
}

dependencies {
    compile group: 'de.sciss', name: 'jwave', version:'1.0.3'
    compile group: 'com.github.samtools', name: 'htsjdk', version:'1.129'
    compile group: 'com.github.broadinstitute', name: 'picard', version:'2.0.1'
    compile group: 'org.apache.commons', name: 'commons-lang3', version:'3.4'
    compile group: 'com.lowagie', name: 'itext', version:'2.1.5'
    compile group: 'com.soulwarelabs.jcommons', name: 'jcommons-api', version:'1.0.0'
    compile group: 'org.apache.commons', name: 'commons-math', version:'2.2'
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version:'2.0'
    compile group: 'commons-cli', name: 'commons-cli', version:'1.3'
    compile group: 'junit', name: 'junit', version:'4.12'
    compile group: 'net.sf.opencsv', name: 'opencsv', version:'2.0'
    compile group: 'com.h2database', name: 'h2', version:'1.4.193'
    compile group: 'commons-logging', name: 'commons-logging', version:'1.2'
    compile group: 'org.apache.poi', name: 'poi', version:'3.8'
    compile group: 'org.apache.poi', name: 'poi-ooxml', version:'3.14'
    compile group: 'org.apache.poi', name: 'ooxml-schemas', version:'1.3'
    compile group: 'org.apache.poi', name: 'poi-contrib', version:'3.6'
    compile group: 'dom4j', name: 'dom4j', version:'1.6.1'
    compile group: 'stax', name: 'stax-api', version:'1.0.1'
    compile group: 'org.jfree', name: 'jfreechart', version:'1.0.19'
    compile group: 'org.apache.commons', name: 'commons-math3', version:'3.6'
    compile fileTree(dir: "dependencies", include: ["*.jar"])
    testCompile group: 'junit', name: 'junit', version: '4.12'
    utilsCompile sourceSets.test.output
}

def commitHash = { ->
    def baos = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = baos
    }
    return baos.toString().trim()
}

task uberjar(type: Jar) {
  baseName = project.name + '-standalone'
  from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
  with jar
  manifest {
    attributes 'Implementation-Title': project.name
    attributes 'Implementation-Version': project.version + '_' + commitHash()
    attributes 'Main-Class': 'jp.ac.utokyo.rcast.karkinos.cmd.KarkinosCmd'
  }
  exclude "META-INF/*.RSA"
  exclude "META-INF/*.DSA"
  exclude "META-INF/*.SF"
}

task utilsJar(type: Jar) {
    from sourceSets.utils.output
}
